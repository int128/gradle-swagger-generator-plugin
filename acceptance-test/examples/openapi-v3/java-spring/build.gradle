plugins {
    id 'java'
    id 'org.hidetake.swagger.generator'
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.1.3.RELEASE'
    implementation 'io.swagger.core.v3:swagger-annotations:2.0.7'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:3.3.4'
}

swaggerSources {
    petstore {
        inputFile = file("$rootDir/openapi-v3-petstore.yaml")
        code {
            language = 'spring'
            configFile = file("$rootDir/java-spring-config.json")
			// see https://github.com/OpenAPITools/openapi-generator/issues/746
            components = ['models','apis','supportingFiles']
        }
    }
}

compileJava.dependsOn swaggerSources.petstore.code
sourceSets.main.java.srcDir "${swaggerSources.petstore.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.petstore.code.outputDir}/src/main/resources"
