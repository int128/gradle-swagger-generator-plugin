plugins {
    id 'base'
    id 'org.hidetake.swagger.generator'
}

repositories {
    // This example assumes the swagger template is published on the Maven local repository.
    // Actually it can be external such as Nexus or Artifactory.
    mavenLocal()

    mavenCentral()
}

dependencies {
    swaggerCodegen localGroovy()
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.30'
    swaggerCodegen 'org.hidetake:swagger-generators:1.0.0'
}

swaggerSources {
    petstore {
        inputFile = file("$rootDir/openapi-v3-petstore.yaml")
        code {
            language = 'generators.MySpring'
            configFile = file("$rootDir/java-spring-config.json")
            components = ['models', 'apis']

            // https://github.com/int128/gradle-swagger-generator-plugin/issues/221
            jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED']
        }
    }
}

build.dependsOn generateSwaggerCode
