plugins {
    id 'groovy-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation parent

    testImplementation platform('org.spockframework:spock-bom:2.3-groovy-4.0')
    testImplementation 'org.spockframework:spock-core'
}

file('projects/settings.gradle').eachLine { line ->
    def m = (line =~ /include '(.+?)'/)
    if (m) {
        def path = m.group(1)
        def escaped = path.replace(':', '-')
        def realPath = "$projectDir/projects/${path.replace(':', '/')}"

        task("build-$escaped", type: JavaExec, group: 'acceptance-test') {
            classpath = sourceSets.test.runtimeClasspath
            mainClass = 'org.hidetake.gradle.swagger.generator.test.Main'
            args = ['-s', "$path:build"]
        }

        project.tasks.clean.delete "$realPath/build"
    }
}

task('example-docs', description: 'Generate example documents', group: 'documentation') {
    dependsOn 'build-ui-v3-basic'
    doLast {
        copy {
            from "$projectDir/projects/ui-v3/basic/build/swagger-ui-petstore"
            into "$buildDir/examples/ui-v3"
        }
    }
    dependsOn 'build-redoc-basic'
    doLast {
        copy {
            from "$projectDir/projects/redoc/basic/build/redoc-petstore"
            into "$buildDir/examples/redoc"
        }
    }
    dependsOn 'build-codegen-v2-html'
    doLast {
        copy {
            from "$projectDir/projects/codegen-v2/html/build/swagger-code-petstore"
            into "$buildDir/examples/html-codegen-v2"
        }
    }
    dependsOn 'build-codegen-v3-html'
    doLast {
        copy {
            from "$projectDir/projects/codegen-v3/html/build/swagger-code-petstore"
            into "$buildDir/examples/html-codegen-v3"
        }
    }
    dependsOn 'build-openapi-v3-html'
    doLast {
        copy {
            from "$projectDir/projects/openapi-v3/html/build/swagger-code-petstore"
            into "$buildDir/examples/html-openapi-v3"
        }
    }
}

test {
    useJUnitPlatform()

    inputs.files fileTree(dir: "$project.projectDir/projects", excludes: ['**/.gradle', '**/build'])
}
