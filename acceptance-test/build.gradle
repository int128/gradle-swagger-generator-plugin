plugins {
    id 'groovy'
    id 'java-gradle-plugin'
}

repositories {
    jcenter()
}

dependencies {
    testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

gradlePlugin {
    pluginSourceSet parent.sourceSets.main
}

file('examples/settings.gradle').eachLine { line ->
    def m = (line =~ /include '(.+?)'/)
    if (m) {
        def path = m.group(1)
        def escaped = path.replace(':', '-')
        def realPath = "$projectDir/examples/${path.replace(':', '/')}"

        task("build-$escaped", type: JavaExec, group: 'acceptance-test') {
            classpath = sourceSets.test.runtimeClasspath
            main = 'org.hidetake.gradle.swagger.generator.test.Main'
            args = ["$path:build"]
        }

        project.tasks.clean.delete "$realPath/build"
    }
}

task('example-docs', description: 'Generate example documents', group: 'documentation') {
    dependsOn 'build-ui-v3-basic'
    doLast {
        copy {
            from "$projectDir/examples/ui-v3/basic/build/swagger-ui-petstore"
            into "$buildDir/examples/ui-v3"
        }
    }
    dependsOn 'build-redoc-basic'
    doLast {
        copy {
            from "$projectDir/examples/redoc/basic/build/redoc-petstore"
            into "$buildDir/examples/redoc"
        }
    }
    dependsOn 'build-codegen-v2-html'
    doLast {
        copy {
            from "$projectDir/examples/codegen-v2/html/build/swagger-code-petstore"
            into "$buildDir/examples/html-codegen-v2"
        }
    }
    dependsOn 'build-codegen-v3-html'
    doLast {
        copy {
            from "$projectDir/examples/codegen-v3/html/build/swagger-code-petstore"
            into "$buildDir/examples/html-codegen-v3"
        }
    }
    dependsOn 'build-openapi-v3-html'
    doLast {
        copy {
            from "$projectDir/examples/openapi-v3/html/build/swagger-code-petstore"
            into "$buildDir/examples/html-openapi-v3"
        }
    }
}


test {
  inputs.files fileTree(dir: "$project.projectDir/examples", excludes: ['**/.gradle', '**/build'])
}
